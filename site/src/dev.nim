## Live-reloading dev server for the isonim-docs self-documentation site.
##
## Serves this site's own `content/` plus its themed assets (`assets/style.css`
## with the Metacraft token CSS prepended, and the `static/` fonts & icons -- the
## same dirs `build.nim` maps into `public/assets/`) over HTTP, and watches
## `content/` so any edit hot-reloads every open tab via the framework's
## `dev_server` WebSocket live-reload channel.
##
## `basePath` (set on the BUILT site for GitHub project-Pages hosting) is a
## `buildSite`-only URL transform; the dev server renders root-relative URLs, so
## everything resolves at http://localhost:<port>/ with no prefix.
##
## Driven by `just dev-docs` (server) + `just open-docs` (browser); optional first arg is
## the port (default 8000).

import std/[os, strutils, asyncdispatch]
import dev_server
import core/docs_tokens
import ./docs_config
import ./theme_tokens

export dev_server

proc newDocsDevServer*(contentDir = "content";
                       assetsDirs = @["assets", "static"]): DevServer =
  ## This site's themed live-reload dev server (own content/config/tokens over
  ## its assets/ + static/ dirs). Exposed so a test drives the exact `just dev-docs`
  ## wiring without binding a socket.
  newDevServer(contentDir = contentDir, cfg = isonimDocsDocsConfig(),
               assetsDirs = assetsDirs,
               docsTokensCss = docsTokensCssLive(),
               tokensCssProvider = (proc(): string = docsTokensCssLive()),
               watchPaths = @[docsDesignSystemPath])

when isMainModule:
  let port = if paramCount() >= 1: parseInt(paramStr(1)) else: 8000
  let server = newDocsDevServer()
  stdout.writeLine "isonim-docs dev server -> http://localhost:" & $port &
    "  (watching content/, live reload on; Ctrl-C to stop)"
  stdout.flushFile()
  waitFor serve(server, port)
